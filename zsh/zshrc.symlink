# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/projects

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

fpath=(~/completions $fpath)

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

ZSH_THEME="robbyrussell"

source $HOME/.oh-my-zsh/oh-my-zsh.sh

# bindkey '^[[A' history-substring-search-up
# bindkey '^[[B' history-substring-search-down

bindkey '^k' history-substring-search-up
bindkey '^j' history-substring-search-down

# Let zsh ignore lines starting with '#'
set -k

# Let zsh kill suspended zobs with 'exit'
setopt NO_HUP
setopt NO_CHECK_JOBS

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
# will override any of public configs
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

source /usr/local/bin/virtualenvwrapper.sh
workon mypy27
